@name Wheel Speed Converter
@inputs [Adv_Ent_Marker]:wirelink 
@outputs [Wheel_Speed, Air_Speed]:number 
@persist [Units, Unit_Per_Hour, MaxBoxSize, MaxRPM, MaxVel, Wheels]:array
@persist [Unit, UpdateFreq]:number
@strict

if (first() | dupefinished())
{
    Unit          = 2 #1 = metric #2 = imperial
    UpdateFreq    = 5    
    
    Units         = array("mm", "in", "km/h", "mph")
    Unit_Per_Hour = array(60 / 1000000, 60 / 63360)
    MaxBoxSize    = array()
    MaxRPM        = array()
    MaxVel        = array()

    if (->Adv_Ent_Marker)
    {
        Wheels = Adv_Ent_Marker["Entities", array]
    }
}
if (Wheels:count() != 0)
{
    foreach (K, V:entity = Wheels)
    {
        MaxBoxSize[K, number] = round(V:boxSize():x())
        MaxRPM[K,     number] = abs(V:angVel():pitch() / 6)
        MaxVel[K,     number] = V:vel():length()
    }
    local BoxSize  = MaxBoxSize:average()
    local RPM      =     MaxRPM:average()
    local Vel      =     MaxVel:average()
    
    local Diameter = toUnit(Units[Unit, string], BoxSize)
    
    Wheel_Speed    = RPM * Diameter * Unit_Per_Hour[Unit, number] * _PI
    Air_Speed      = toUnit(Units[Unit + 2, string], Vel)

    interval(1000 / UpdateFreq)
}
